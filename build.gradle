plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.5.0'
}

group = "de.siegmar"
version = "1.1.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    testImplementation 'com.google.guava:guava:28.1-jre'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.10.1'
}

test {
    useJUnitPlatform()
}

tasks.matching {task -> task.name.startsWith('spotbugs')}.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "logback-awslogs-json-encoder"
            from components.java

            pom {
                name = 'Logback awslogs JSON encoder'
                description = 'Logback encoder for producing JSON formatted log output for Amazon CloudWatch Logs Insights.'
                url = 'https://github.com/osiegmar/logback-awslogs-json-encoder'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License version 2.1'
                        url = 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }
                scm {
                    url = 'https://github.com/osiegmar/logback-awslogs-json-encoder'
                    connection = 'scm:git:https://github.com/osiegmar/logback-awslogs-json-encoder.git'
                }
                developers {
                    developer {
                        id = 'osiegmar'
                        name = 'Oliver Siegmar'
                        email = 'oliver@siegmar.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
